#Область ПрограммныйИнтерфейс

// Инициализация.
// 
// Параметры:
//  Этот - Структура - Текущий объект
Процедура Инициализация(Этот) Экспорт

	// TODO: нужно чтобы эти свойства были немутабельными.
	Этот.Вставить("ПрограммныйИнтерфейс", Новый Массив());
	Этот.Вставить("Переменные", Новый Массив());
	Этот.Вставить(
		"Перем",
		Обработки.ОП_МетодОбъекта.Метод("НоваяПеременная", ОП_Класс, Этот)
	);
	Этот.Вставить(
		"Метод",
		Обработки.ОП_МетодОбъекта.Метод("НовыйМетод", ОП_Класс, Этот)
	);
	
КонецПроцедуры

// Добавляет описание переменной экземпляра класса
// 
// Параметры:
//  Этот - Структура - Текущий объект
//  Имя - Строка - Имя переменной
//  ЗначениеПоУмолчанию - Произвольный - Значение по умолчанию
// 
// Возвращаемое значение:
//  Структура
Функция НоваяПеременная(Этот, Имя, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Переменная = Переменная();
	Переменная.Имя = Имя;
	Переменная.ЗначениеПоУмолчанию = ЗначениеПоУмолчанию;
	Этот.Переменные.Добавить(Переменная);
	Возврат Переменная;

КонецФункции

// Добавляет описание метода
// 
// Параметры:
//  Этот - Структура - Текущий объект
//  Имя - Строка - Имя переменной
Процедура НовыйМетод(Этот, Имя) Экспорт
	
	Метод = Метод();
	Метод.Имя = Имя;
	Этот.ПрограммныйИнтерфейс.Добавить(Метод);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Переменная()

	Переменная = Новый Структура();
	ОП_Переменная.Инициализация(Переменная);
	Возврат Переменная;

КонецФункции

Функция Метод()
	
	Метод = Новый Структура();
	Метод.Вставить("Имя", "");
	Возврат Метод;
	
КонецФункции

#КонецОбласти