#Область ПрограммныйИнтерфейс

// Создает класс по описанию в указанном модуле.
// 
// Параметры:
//  Модуль - ОбщийМодуль
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
//  * Новый - ОбработкаОбъект.ОП_МетодОбъекта - конструктор
//  * Перем - ОбработкаОбъект.ОП_МетодОбъекта - задает поле объекта
//  * ПрограммныйИнтерфейс - Массив из см. ОП_Класс.НовыйМетод
//  * Переменные - Массив из см. ОП_Класс.НоваяПеременная
//  * Модуль - ОбщийМодуль - модуль с кодом для класса
//  * Родитель - см. ОП_Классы.Класс
Функция Класс(Модуль) Экспорт
	
	Класс = ОП_КлассыСлужебный.Базовый(Новый Структура());
	
	ОП_Класс.Инициализация(Класс);
	
	Класс.Вставить("Модуль", Модуль);
	
	Если Не ОП_Рефлексия.МетодОбъектаСуществует(Модуль, "Инициализация") Тогда
		ВызватьИсключение("Инициализация для класса не определена");
	КонецЕсли;

	//@skip-check dynamic-access-method-not-found
	Модуль.Инициализация(Класс);
	
	Метод = Обработки.ОП_МетодОбъекта.Конструктор(Модуль);
	Класс.Вставить("Новый", Метод);
	
	Финальный = Новый ФиксированнаяСтруктура(Класс);

	Метод.УстановитьКласс(Финальный);

	Возврат Финальный;

КонецФункции

#КонецОбласти
