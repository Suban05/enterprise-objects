#Область ПрограммныйИнтерфейс

// Инициализация.
// 
// Параметры:
//  Этот - Структура
Процедура Инициализация(Этот) Экспорт
			
	Этот.Вставить("Имя", "");
	Этот.Вставить(
		"Переопределяет",
		Обработки.ОП_МетодОбъекта.Метод("Переопределить", ОП_Метод, Этот)
	);
	Этот.Вставить("РасширяемыйМетод", "");
	
КонецПроцедуры

// Указывает, что метод экземпляра переопределяется.
// Если этот метод был вызван в Инициализации, то тогда будет возможность
// в расширяемом методе вызывать метод родителя. Например:
// 
// Процедура Инициализация(Этот) Экспорт
//     Этот.Расширяет._(БазовыйКласс);
//     Этот.Метод._("БазовыйМетод").Переопределяет._("БазовыйМетод");
// КонецПроцедуры
// 
// Процедура БазовыйМетод(Этот) Экспорт
//     // какой-то код...
//     Этот.ПродолжитьВызов._();
// КонецПроцедуры
// 
// Параметры:
//  Этот - см. ОП_Класс.НовыйМетод
//  Метод - Строка
Процедура Переопределить(Этот, Метод) Экспорт
	
	Если Этот.Класс.Родитель = Неопределено Тогда
		ВызватьИсключение("Нельзя переопределить метод, если не указан родитель");
	КонецЕсли;
	
	Если НайтиМетодЭкземпляра(Этот.Класс.Родитель, Метод) = Неопределено Тогда
		ВызватьИсключение(СтрШаблон("Метод ""%1"" не обнаружен в родителе", Метод));
	КонецЕсли;
	Этот.РасширяемыйМетод = Метод;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Найти метод экземпляра.
// 
// Параметры:
//  Класс - см. ОП_Классы.Класс
//  Имя - Строка
// 
// Возвращаемое значение:
//  Структура - см. ОП_Класс.НовыйМетод
Функция НайтиМетодЭкземпляра(Класс, Имя)
	
	Для каждого Метод Из Класс.ПрограммныйИнтерфейс Цикл
		Если Метод.Имя = Имя Тогда
			Возврат Метод;
		КонецЕсли;
	КонецЦикла;

КонецФункции

#КонецОбласти
