#Область ПрограммныйИнтерфейс

// Вызывает процедуру объекта.
// 
// Параметры:
//  Объект - Произвольный - Объект
//  Метод - Строка - Метод
//  Аргументы - Массив из Произвольный - Аргументы
Процедура ВызватьПроцедуруОбъекта(Объект, Метод, Аргументы) Экспорт
	ОП_ОбщегоНазначения.ВыполнитьМетодОбъекта(Объект, Метод, Аргументы);
КонецПроцедуры

// Вызывает функцию объекта.
//
// Параметры:
//  Объект - Произвольный - Объект
//  Метод - Строка - Метод
//  Аргументы - Массив из Произвольный - Аргументы
//
// Возвращаемое значение:
//  Произвольный
Функция ВызватьФункциюОбъекта(Объект, Метод, Аргументы) Экспорт
	Возврат ОП_ОбщегоНазначения.ВызватьФункциюОбъекта(Объект, Метод, Аргументы);
КонецФункции

// Проверяет существование метода объекта.
// 
// Параметры:
//  Объект - Произвольный - Объект
//  Метод - Строка - Метод
// 
// Возвращаемое значение:
//  Булево - Метод объекта существует
Функция МетодОбъектаСуществует(Объект, Метод) Экспорт
	
	Ошибка = ИмитированныйВызовМетода(Объект, Метод);
	Возврат МетодСуществует(Ошибка, Метод);
	
КонецФункции

// Проверяет существование свойства объекта.
// 
// Параметры:
//  Объект - Произвольный - Объект
//  Свойство - Строка - Свойство
// 
// Возвращаемое значение:
//  Булево - Свойство объекта существует
Функция СвойствоОбъектаСуществует(Объект, Свойство) Экспорт
	
	Возврат 
		ТипЗнч(ПолучитьСвойствоОбъекта(Объект, Свойство)) 
		<> Тип("ИнформацияОбОшибке");		
	
КонецФункции

// Получает значение свойства объекта по ключу.
// 
// Параметры:
//  Объект - Произвольный - Объект
//  Свойство - Строка - Свойство
// 
// Возвращаемое значение:
//  Произвольный - Получить свойство объекта
Функция ПолучитьСвойствоОбъекта(Объект, Свойство) Экспорт
	
	Попытка
		Возврат Объект[Свойство];
	Исключение
		Возврат ИнформацияОбОшибке();
	КонецПопытки;		
	
КонецФункции	

// Проверяет, является ли метод объекта процедурой.
// 
// Параметры:
//  Объект - Произвольный
//  Метод - Строка
// 
// Возвращаемое значение:
//  Булево - Это процедура объекта
Функция ЭтоПроцедураОбъекта(Объект, Метод) Экспорт
	
	Возврат ЭтоОбращениеКПроцедуреКакКФункции(
		ИмитированныйВызовМетода(Объект, Метод)
	);

КонецФункции
	
#КонецОбласти	

#Область СлужебныеПроцедурыИФункции

Функция ИмитированныйВызовМетода(Объект, Метод)
	Аргументы = Новый Массив(15);
	Возврат ВыполнитьМетод(Объект, Метод, Аргументы);
КонецФункции

Функция МетодСуществует(Ошибка, ИмяМетода)
	
	Возврат СтрНайти(НРег(Ошибка.Описание), "(" + НРег(ИмяМетода) + ")") = 0
		Или ЭтоОбращениеКПроцедуреКакКФункции(Ошибка);
	
КонецФункции

Функция ЭтоОбращениеКПроцедуреКакКФункции(Ошибка)
	Возврат СтрНайти(НРег(Ошибка.Описание), "обращение к процедуре объекта как к функции") <> 0;
КонецФункции

Функция ВыполнитьМетод(Объект, Метод, Аргументы)
	
	Попытка
		ОП_ОбщегоНазначения.ВызватьФункциюОбъекта(Объект, Метод, Аргументы);
	Исключение
		Возврат ИнформацияОбОшибке();
	КонецПопытки;
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти	